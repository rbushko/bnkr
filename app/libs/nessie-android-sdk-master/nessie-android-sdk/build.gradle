apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


// Version to use when deploying the artifact
version = "1.0.4"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    resourcePrefix "nessie__"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def gitUrl = 'https://github.com/nessieisreal/nessie-android-sdk'   // Git repository URL
group = "com.reimaginebanking.api"

install {
    repositories.mavenInstaller {
        // Generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // library description
                name 'Android SDK for the Nessie API.'

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile "com.github.tomakehurst:wiremock:2.1.9"
    testCompile 'org.json:json:20140107'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0-RC1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "NessieAndroidSDK"
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
    }
}